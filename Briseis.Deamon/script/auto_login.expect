#!/usr/bin/expect -f
#
#    auto_login.exp,
#
#         an ssh auto login expect script. support run command, ssh connect test and interact mode.
#
#    Created by Ruoyan Wong(@saipanno) on 2012/12/10.
#


# silly globals
set PROMPT "(%|>|#|\\\$) $"

# 解析参数,存入数组中.
for {set x 0} {$x<$argc} {incr x 2} {
    set opt [lindex $argv $x]
    set nexts [expr {$x + 1}]
    switch -glob $opt {
        "u" { set config(user)    [lindex $argv $nexts] }
        "p" { set config(port)    [lindex $argv $nexts] }
        "f" { set config(script)  [lindex $argv $nexts] }
        "i" { set config(secret)  [lindex $argv $nexts] }
        "s" { set config(shadow)  [lindex $argv $nexts] }
        "a" { set config(address) [lindex $argv $nexts] }
        "d" { set config(logdir)  [lindex $argv $nexts] }
        "t" { set config(time)    [lindex $argv $nexts] }
        "o" { set config(operate) [lindex $argv $nexts] }
        default {incr x -1}
    }
    if { [expr {$nexts + 1}] > $argc } break 
}

# define the command-line script usage.
set help "Usage: auto_login.expect \[arguments\]: \n
required arguments:
    o  OPERATE operate type, (support: run, test, interact)
    a  ADDRESS server address
    d  LOGDIR  syslog directory
    u  USER    user
    p  PORT    port
    f  SCRIPT  script file 
    i  SECRET  user identity file
    s  SHADOW  password file
    t  TIMEOUT expect build-in timeout"

# check the command-line script arguments.
if { $argc==1 && [lindex $argv 0]=="help" } {
    puts $help
    exit
} elseif { ![info exists config(address)] } {
    puts "Error parameters: -a\n"
    puts $help
    exit
} elseif { ![info exists config(logdir)] || ![file exists $config(logdir)] } { 
    puts "Error parameters: -d\n"
    puts $help
    exit
} elseif { ![info exists config(operate)] || ($config(operate) != "run" && $config(operate) != "test" && $config(operate) != "interact") } {
    puts "Error parameters: -o\n"
    puts $help
    exit
} elseif { $config(operate) == "run" && ![file exists $config(script)]} {
    puts "Error parameters: -o\n"
    puts $help
    exit
} elseif { [info exists config(secret)] && ![file exists $config(secret)] } { 
    puts "Error parameters: -i\n"
    puts $help
    exit
}

# close console output when the operate is run or test.
if { $config(operate) == "run" || $config(operate) == "test" } {
    log_user 0
}

# define expect build-in timeout
if { [info exists config(time)] } {
    set timeout $config(time)
}

# read password from shadow file. otherwise define an empty array.
if { [info exists config(shadow)] && [file exists $config(shadow)] } { 
    set count 0
    set file [open $config(shadow) r]
    while { [ gets $file oneline ]>=0 } { 
        set password($count) $oneline
        incr count
    }   
    close $file
} else {
    array set password []
}

# open the script file if the operate is run. 
if { $config(operate) == "run" } { 
    set file [open $config(script) r]
} 

# according user input script parameter set ssh command opts. 
set COMMAND "ssh -o StrictHostKeyChecking=no -o GSSAPIAuthentication=no -o VerifyHostKeyDNS=no"
foreach {key value} [array get config] {
    if {$key == "user"} { 
        append COMMAND " " "-l $value" 
    } elseif {$key == "port"} { 
        append COMMAND " " "-p $value"
    } elseif {$key == "secret"} {
        append COMMAND " " "-i $value"
    }   
}

set count 0
eval spawn $COMMAND $config(address)
expect {
    # If match keyword "Permission denied (publickey,gssapi-with-mic,password).", quit with authentication failed status.
    "Permission denied (*)" {
        set status "nopasswd"
        if { $config(operate) == "run" || $config(operate) == "test" } {
            set file [open $config(logdir)/$status.stat a]
            puts $file "$config(address)"
            close $file
        } elseif { $config(operate) == "interact" } {
            puts "Authentication failed!\n"
        }
        exit
    }
    # If match keyword "Permission denied", jump to the next password.
    "Permission denied" {
        incr count
        exp_continue
    }
    # Send current password. Quit if there is no record.
    "*?assword:*" {
        if [info exists password($count)] {
            send "$password($count)\r"
            exp_continue
        } else {
            # fuck! Match authentication failed!
            set status "nopasswd"
            if { $config(operate) == "run" || $config(operate) == "test" } {
                set file [open $config(logdir)/$status.stat a]
                puts $file "$config(address)"
                close $file
            } elseif { $config(operate) == "interact" } {
                puts "Authentication failed!\n"
            }
            exit
        }
    }
    "No route to host" {
        set status "noroute"
    }
    "Connection refused" {
        set status "refused"
    }
    "Connection closed" {
        set status "closed"
    }
    "Connection reset" {
        set status "reset"
    }
    "Connection timed out" {
        set status "timeout"
    }
    -re "$PROMPT" {
        set status "success" 
        # yeah! Do whatever you want to do.
        if { $config(operate) == "run" } {
            while { [ gets $file CMD ]>=0 } { 
                send -- "$CMD\n"
            } 
            send -- "exit\n"
        } elseif { $config(operate) == "test" }  {
            send -- "exit\n"
        } elseif { $config(operate) == "interact" } {
            interact
            exit
        } 
    }
    timeout {
        set status "timeout"
        # haha! Match expect build-in timeout.
        if { $config(operate) == "run" || $config(operate) == "test" } {
            set file [open $config(logdir)/$status.stat a]
            puts $file "$config(address)"
            close $file
        } elseif { $config(operate) == "interact" }  {
            puts "Trigger expect timeout"
        }
        exit
    }
}

# ha! Match all nomal expect eof.
expect eof {
    if { $config(operate) == "run" } {
        if { $status == "success" } {
            set file [open $config(logdir)/$config(address).txt a]
            puts $file "$expect_out(buffer)"
        } else {
            set file [open $config(logdir)/$status.stat a]
            puts $file "$config(address)"
        }
    } elseif { $config(operate) == "test" }  {
        set file [open $config(logdir)/$status.stat a]
        puts $file "$config(address)"
    } elseif { $config(operate) == "interact" }  {
        puts "Trigger exception: $status"
    }
    if { [info exists file1] } { close $file }
    exit
}
